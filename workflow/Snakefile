'''Snakefile for GWAS Variant and Sample QC Version 0.3.1'''
# import ipdb; ipdb.set_trace()
from scripts.parse_config_GWASampleFiltering import parser
import os
import warnings

configfile: "config/config.yaml"
config['full_pipeline'] = True
config['king'] = True

shell.executable("/bin/bash")

BPLINK = ["bed", "bim", "fam"]
RWD = os.getcwd()


if 'is_module' in config and config['is_module']:
    start = config['start']
    SAMPLE = config['SAMPLE']
    DATAOUT = config['DATAOUT']
else:
    start, SAMPLE, DATAOUT = parser(config)


FAMILY = 'T' if config['family'] else 'F'
istg = config['isTG'] if 'isTG' in config else False


def enableqc(qc_type):
    if 'qc' in config:
        if (isinstance(config['qc'], list)
                and all([isinstance(x, str) for x in config['qc']])):
            return qc_type in config['qc']
        elif isinstance(config['qc'], dict):
            if (qc_type in config['qc']
                    and isinstance(config['qc'][qc_type], bool)):
                return config['qc'][qc_type]
            else:
                raise Exception(
                    "Malformed QC dict: All supported QC must be present")
        else:
            raise Exception("Malformed QC list: Please provide dict OR list")
    else:
        return True


qc_type = ['variant', 'callrate', 'sex', 'heterozygosity',
           'relatedness', 'ancestry', 'popstrat']
qc_type = {x: enableqc(x) for x in qc_type}

def flatten(nested):
    flat = []
    for el in nested:
        if not isinstance(el, list):
            flat.append(el)
        else:
            flat += flatten(el)
    return flat


outs = {
    "report": expand("{dataout}/stats/{sample}_GWAS_QC.html", sample=SAMPLE, dataout = DATAOUT),
    "exclusions": expand("{dataout}/{sample}_exclude.samples", sample=SAMPLE, dataout = DATAOUT),
    "filtered": expand("{dataout}/{sample}_Excluded.{ext}",
                       sample=SAMPLE, ext=BPLINK, dataout = DATAOUT)}

outputs = [outs[x] for x in config["outputs"]]
outputs = flatten(outputs)

rule all:
    input:
        outputs

relatedness_included = False

if qc_type['variant'] or qc_type['callrate']:
    if qc_type['callrate'] and not qc_type['variant']:
        raise Exception("Sample call-rate QC cannot be run without variant QC")
    include: 'rules/variant_qc.smk'

if qc_type['callrate']:
    sampleqc_in_plink = rules.sample_callRate.output[0]
    sampleqc_in_plink_stem = rules.sample_callRate.params.out
elif qc_type['variant']:
    sampleqc_in_plink = expand("{{dataout}}/{{sample}}_SnpQc.{ext}", ext=BPLINK)
    sampleqc_in_plink_stem = rules.snp_qc.params.out
else:
    sampleqc_in_plink = start['files']
    sampleqc_in_plink_stem = start['stem']

if qc_type['ancestry']:
    include: 'rules/ancestry.smk'

if qc_type['popstrat']:
    if config['pcair']:
        if not qc_type['relatedness']:
            warnings.warn("PCAiR requires relatedness QC. Enabling.")
            qc_type['relatedness'] = True
        include: 'rules/relatedness.smk'
        relatedness_included = True
    include: 'rules/stratification.smk'

if qc_type['relatedness'] and not relatedness_included:
    include: 'rules/relatedness.smk'
    relatedness_included = True

# ---- Exclude Samples with discordant sex ----
#  Use ADNI hg18 data, as the liftover removed the x chromsome data


rule sexcheck_QC:
    input: start['sex']
    output:
        "{dataout}/{sample}_SexQC.sexcheck"
    params:
        indat = start['sex_stem'],
        out = "{dataout}/{sample}_SexQC",
    conda: "envs/plink.yaml"
    shell:
        '''
plink --keep-allele-order --bfile {params.indat} \
  --check-sex --aec --out {params.out}
'''

rule sex_sample_fail:
    input: rules.sexcheck_QC.output
    output: "{dataout}/{sample}_exclude.sexcheck"
    container: 'docker://befh/r_env_gwasamplefilt:3'
    script: 'scripts/sexcheck_QC.R'


# ---- Exclude Samples with outlying heterozigosity ----
rule heterozygosity_QC:
    input: sampleqc_in_plink
    output: "{dataout}/{sample}_HetQC.het"
    params:
        indat_plink = sampleqc_in_plink_stem,
        out = "{dataout}/{sample}_HetQC"
    conda: "envs/plink.yaml"
    shell: "plink --keep-allele-order --bfile {params.indat_plink} --het --out {params.out}"

rule heterozygosity_sample_fail:
    input: rules.heterozygosity_QC.output
    output: "{dataout}/{sample}_exclude.heterozigosity"
    container: 'docker://befh/r_env_gwasamplefilt:3'
    script: 'scripts/heterozygosity_QC.R'


rule SampleExclusion:
    input:
        SampCallRate = "{dataout}/{sample}_callRate.irem" if qc_type['callrate'] else '/dev/null',
        het = "{dataout}/{sample}_exclude.heterozigosity" if qc_type['heterozygosity'] else '/dev/null',
        sex = "{dataout}/{sample}_exclude.sexcheck" if qc_type['sex'] else '/dev/null',
        pca = "{dataout}/{sample}_exclude.pca" if qc_type['ancestry'] else '/dev/null',
        relat = "{dataout}/{sample}_exclude.relatedness" if qc_type['relatedness'] else '/dev/null'
    output:
        out = "{dataout}/{sample}_exclude.samples",
        out_distinct = "{dataout}/{sample}_exclude.distinct_samples"
    container: 'docker://befh/r_env_gwasamplefilt:3'
    script: "scripts/sample_QC.R"

rule Exclude_failed:
    input:
        plink = sampleqc_in_plink,
        indat_exclude = rules.SampleExclusion.output.out_distinct
    output:
        temp(expand("{{dataout}}/{{sample}}_Excluded.{ext}", ext=BPLINK, dataout = DATAOUT)),
        excl = temp('{dataout}/{sample}_exclude.plink')
    params:
        indat_plink = sampleqc_in_plink_stem,
        out = "{dataout}/{sample}_Excluded"
    conda: "envs/plink.yaml"
    shell:
        '''
cat {input.indat_exclude} | sed '1d' | cut -d' ' -f1,2 > {output.excl}
plink --keep-allele-order --bfile {params.indat_plink} --remove {output.excl} \
--make-bed --out {params.out}
'''

rule GWAS_QC_Report:
    input:
        SexFile = "{dataout}/{sample}_SexQC.sexcheck" if qc_type['sex'] else '/dev/null',
        hwe = "{dataout}/{sample}_SnpQc.hwe" if qc_type['variant'] else '/dev/null',
        frq = "{dataout}/{sample}_SnpQc.frq" if qc_type['variant'] else '/dev/null',
        frqx = "{dataout}/{sample}_SnpQc.frqx" if qc_type['variant'] else '/dev/null',
        imiss = "{dataout}/{sample}_callRate.imiss" if qc_type['callrate'] else '/dev/null',
        HetFile = "{dataout}/{sample}_HetQC.het" if qc_type['heterozygosity'] else '/dev/null',
        IBD_stats = "{dataout}/{sample}_IBDQC.Rdata" if qc_type['relatedness'] else '/dev/null',
        PCA_rdat = "{dataout}/{sample}_pca.Rdata" if qc_type['ancestry'] else '/dev/null',
        cluster_pops = "{dataout}/{sample}_cluster_pops.tsv" if qc_type['ancestry'] else '/dev/null',
        PopStrat_eigenval = "{dataout}/{sample}_filtered_PCA.eigenval" if qc_type['popstrat'] else '/dev/null',
        PopStrat_eigenvec = "{dataout}/{sample}_filtered_PCA.eigenvec" if qc_type['popstrat'] else '/dev/null',
        partmethod = rules.PCAPartitioning.output[1] if qc_type['popstrat'] and config["pcair"] else "/dev/null"
    output:
         "{dataout}/stats/{sample}_GWAS_QC.html"
    params:
        rwd = RWD,
        Family = FAMILY,
        pi_threshold = 0.1875,
        output_dir = "{dataout}/stats",
        idir = "{dataout}/stats/md/{sample}",
        geno_miss = config['QC']['GenoMiss'],
        samp_miss = config['QC']['SampMiss'],
        MAF = config['QC']['MAF'],
        HWE = config['QC']['HWE'],
        superpop = config['superpop'],
        partmethod = rules.PCAPartitioning.output[1] if qc_type['popstrat'] and config["pcair"] else "none"
    container: 'docker://befh/r_env_gwasamplefilt:3'
    script: "scripts/GWAS_QC.Rmd"
